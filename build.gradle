plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT'
	id 'com.github.johnrengelman.shadow' version '7.1.+'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
archivesBaseName = "${mod_name}-${minecraft_version}"

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
	implementation "ch.obermuhlner:big-math:2.3.+"
	shadow "ch.obermuhlner:big-math:2.3.+"
}

loom {
	accessWidenerPath = file("src/main/resources/kardexotools.accesswidener")
	mixin.defaultRefmapName = "${mod_id}.refmap.json"
}

processResources {
	outputs.upToDateWhen {
		false
	}
	
	inputs.property "version", project.version
	
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

shadowJar {
	configurations = [project.configurations.compileClasspath]
	from sourceSets.main.output
	duplicatesStrategy = 'exclude'
	exclude('META-INF/**')
	relocate 'ch.obermuhlner', 'net.kardexo.kardexotools.include.ch.obermuhlner'
	classifier 'dev'
}

remapJar {
	dependsOn(shadowJar)
	input.set shadowJar.archiveFile
}
