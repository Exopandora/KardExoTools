plugins {
	id 'fabric-loom' version '0.11-SNAPSHOT'
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '7.1.+'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

configurations {
	shade
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	implementation "ch.obermuhlner:big-math:2.3.+"
	shade "ch.obermuhlner:big-math:2.3.+"
}

loom {
	accessWidenerPath = file("src/main/resources/kardexotools.accesswidener")
}

processResources {
	inputs.property "version", project.version
	
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

shadowJar {
	configurations = [project.configurations.shade]
	from sourceSets.main.output
	duplicatesStrategy = 'exclude'
	exclude('META-INF/**')
	relocate 'ch.obermuhlner' , 'net.kardexo.kardexotools.include.ch.obermuhlner'
	classifier 'dev'
}

remapJar {
	dependsOn(shadowJar)
	input.set shadowJar.archiveFile
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
