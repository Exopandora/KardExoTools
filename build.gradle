plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT'
	id 'com.github.johnrengelman.shadow' version '7.1.+'
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
archivesBaseName = "${mod_name}-${minecraft_version}"

configurations {
	shade.extendsFrom(implementation)
	compileClasspath.extendsFrom(shade)
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
	shade "ch.obermuhlner:big-math:${big_math_version}"
	shade "org.apache.commons:commons-compress:${commons_compress_version}"
}

loom {
	accessWidenerPath = file("src/main/resources/kardexotools.accesswidener")
	mixin.defaultRefmapName = "${mod_id}.refmap.json"
}

processResources {
	outputs.upToDateWhen {
		false
	}
	
	inputs.property "version", project.version
	
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

shadowJar {
	configurations = [project.configurations.shade]
	from sourceSets.main.output
	duplicatesStrategy = 'include'
	classifier 'dev'
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
	target = tasks.shadowJar
	prefix = "net.kardexo.kardexotools.include"
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

remapJar {
	dependsOn(shadowJar)
	input.set shadowJar.archiveFile
}
