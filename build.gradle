plugins {
	id 'fabric-loom' version '1.4.+'
	id 'com.github.johnrengelman.shadow' version '7.1.+'
	id 'me.hypherionmc.cursegradle' version '2.+'
}

repositories {
	maven {
		url 'https://masa.dy.fi/maven'
	}
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
javadoc.options.addStringOption('Xdoclint:none', '-quiet')

archivesBaseName = "${mod_name}-${minecraft_version}"
version = mod_version

configurations {
	shade.extendsFrom(implementation)
	compileClasspath.extendsFrom(shade)
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
	modImplementation "carpet:fabric-carpet:${carpet_version}"
	shade "ch.obermuhlner:big-math:${big_math_version}"
	shade "org.apache.commons:commons-compress:${commons_compress_version}"
}

loom {
	accessWidenerPath = file("src/main/resources/kardexotools.accesswidener")
	mixin.defaultRefmapName = "${mod_id}.refmap.json"
}

processResources {
	outputs.upToDateWhen {
		false
	}
	
	inputs.property "version", project.version
	
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

shadowJar {
	configurations = [project.configurations.shade]
	from sourceSets.main.output
	duplicatesStrategy = 'include'
	archiveClassifier = 'dev'
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
	target = tasks.shadowJar
	prefix = "net.kardexo.kardexotools.include"
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

remapJar {
	dependsOn(shadowJar)
	input.set shadowJar.archiveFile
}

curseforge {
	apiKey = project.hasProperty("curse_api_key") ? curse_api_key : ''
	project {
		id = curse_project_id
		changelog = file('./changelog.txt').canRead() ? file('./changelog.txt').text : ''
		changelogType = 'text'
		releaseType = 'release'
		addGameVersion 'Fabric'
		compatible_minecraft_versions.split(",").each {
			addGameVersion(it)
		}
		mainArtifact(remapJar) {
			displayName = "${mod_name}-${minecraft_version}-${mod_version}"
		}
	}
	options {
		javaVersionAutoDetect = false
		forgeGradleIntegration = false
	}
}
